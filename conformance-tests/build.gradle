import org.gradle.jvm.tasks.Jar

plugins {
    id 'java'
    id 'distribution'
}

group 'org.jspecify.conformance'
version '0.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    // Dependencies required by the assertions
    deps {
        java {}
    }
    // Java files with assertions
    assertions {
        java {
            compileClasspath += sourceSets.deps.output
        }
    }
}

tasks.register('conformanceTestSuite', Zip) {
    from sourceSets.assertions.java // assertion files
    from depsJar // deps JAR
    from configurations.assertionsRuntimeClasspath // other dependencies
}

java {
    // Source JAR artifact for assertions
    registerFeature("assertions") {
        usingSourceSet(sourceSets.assertions)
        withSourcesJar()
    }
    // JAR artifact for dependencies required by the assertions
    registerFeature("deps") {
        usingSourceSet(sourceSets.deps)
        withSourcesJar()
    }
}

distributions {
    conformanceTests {
        contents {
            from(sourceSets.assertions.java) {
                into '/assertions'
            }
            from (depsJar) {
                into '/deps'
            }
            from (configurations.assertionsRuntimeClasspath) {
                into '/deps'
            }
        }
    }
}

dependencies {
    assertionsImplementation "org.jspecify:jspecify:0.0.0-SNAPSHOT"
    depsImplementation "org.jspecify:jspecify:0.0.0-SNAPSHOT"
}

// Make sure assertions compile.
check.dependsOn(compileAssertionsJava)
